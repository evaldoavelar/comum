unit TestDaoBase;
{

  Delphi DUnit Test Case
  ----------------------
  This unit contains a skeleton test case class generated by the Test Case Wizard.
  Modify the generated code to correctly setup and call the methods from the unit
  being tested.

}

interface

uses Firedac.VCLUI.Wait,
  TestFramework, System.SysUtils, System.DateUtils, Model.ModelBase, Dao.Base, Dao.IConection,
  System.Classes, Dao.Conection.Firedac, Data.DB, System.Rtti, Dao.Conection.Parametros,
  Database.SGDB, Test.Model.Produto, Test.Model.Pedido, Test.Model.Item, System.Generics.Collections,
  Winapi.Windows, Model.IModelBase, Test.Model.TMOV;

type
  // Test methods for class TDaoBase

  TestTDaoBase = class(TTestCase)
  strict private
    FDaoBase: TDaoBase;
    Models: TList<TModelBase>;
  private
    procedure CheckProdutos(Produto: TProduto; ProdutoBD: TProduto);
    procedure CheckItemPedido(Item: TItemPedido; ItemBD: TItemPedido);

  public
    procedure SetUp; override;
    procedure TearDown; override;
  published
    procedure GetProdutoSQLBuilder;

    procedure GetProdutoDoisParametros;
    procedure GetProdutoMaisDeDoisParametros;
    procedure GetProdutoSemParametros;
    procedure GetProdutoUmParametros;

    procedure PodeInserirProduto;
    procedure PodeAtualizarProdutoSQlBuilder;
    procedure PodeAtualizarProduto;
    procedure PodeExcluirProduto;
    procedure PodeExcluirProdutoSQLBuilder;

    procedure PodeRetornarClientDataSet;

    procedure PodeDarSelectEmObjetoSemAnotacao;

    procedure PodeInserirAtualizarApagarPedido;
  end;

implementation

uses
  Seed;

procedure TestTDaoBase.SetUp;
var
  conexao: TFiredacConection;

begin
  conexao := TFiredacConection.Create(
    TConectionParametros.Create(
    tpSqlServer,
    'LOCALHOST',
    'testeComum',
    'rm',
    'rm',
    'Teste',
    0
    )
    );

  FDaoBase := TDaoBase.Create(conexao);
  Models := TList<TModelBase>.Create;
end;

procedure TestTDaoBase.TearDown;
var
  Model: TModelBase;
begin

  // deletar do banco  os models que ficaram na memoria
  for Model in Models do
  begin
    if (Model is TProduto) then
      FDaoBase.Delete<TProduto>(TProduto(Model))
    else if (Model is TPedido) then
      FDaoBase.Delete<TPedido>(TPedido(Model))
    else if (Model is TItemPedido) then
      FDaoBase.Delete<TItemPedido>(TItemPedido(Model));
  end;

  Models.Free;
  Models := nil;

  FDaoBase.Free;
  FDaoBase := nil;
end;

procedure TestTDaoBase.GetProdutoDoisParametros;
var
  Produto: TProduto;
begin
  Produto := FDaoBase.SelectALL<TProduto>()
    .Where(['CODIGO', 'UND'], ['123456', 'UN'])
    .Get();

end;

procedure TestTDaoBase.GetProdutoSemParametros;
var
  Produto: TList<TProduto>;
begin
  Produto := FDaoBase
    .SelectALL<TProduto>()
    .ToList();
end;

procedure TestTDaoBase.GetProdutoSQLBuilder;
var
  Produtos: TList<TProduto>;
  Produto1: TProduto;
  Produto2: TProduto;
  Produto3: TProduto;
begin
  // ambiente
  Produto1 := TSeed.ProdutoTeste();
  Produto1.DESCRICAO := 'Descricao Teste';
  FDaoBase.Insert<TProduto>(Produto1);

  Produto2 := TSeed.ProdutoTeste();
  Produto2.DESCRICAO := 'Descricao Teste';
  FDaoBase.Insert<TProduto>(Produto2);

  Produto3 := TSeed.ProdutoTeste();
  Produto3.DESCRICAO := 'Descricao Teste';
  FDaoBase.Insert<TProduto>(Produto3);

  // ação
  Produtos :=
    FDaoBase.Select<TProduto>()
    .Column('DESCRICAO')
    .From('PRODUTO')
    .Where('DESCRICAO').Like('Descricao Teste')
    .&And('UND').Equal('UN')
    .&And('DATA_CADASTRO').GreaterOrEqual(date)
    .&And('PRECO_VENDA').Greater(11.99)
    .&And('PRECO_ATACADO').LessOrEqual(100.50)
    .&Or('BLOQUEADO').Equal(False)
    .OrderBy('DATA_CADASTRO')
    .ToList();

  CheckEquals(FDaoBase.LastSql, 'Select ' + #13#10 + ' DESCRICAO' + #13#10 +
    ' From PRODUTO Where (DESCRICAO Like :DESCRICAO) And (UND = :UND) And (DATA_CADASTRO >= :DATA_CADASTRO) And (PRECO_VENDA > :PRECO_VENDA) And (PRECO_ATACADO <= :PRECO_ATACADO) Or (BLOQUEADO = :BLOQUEADO) Order By DATA_CADASTRO');

  CheckTrue(Produtos.Count >= 3);
  Produtos.Free;
end;

procedure TestTDaoBase.GetProdutoUmParametros;
var
  Produto: TProduto;
begin
  Produto := FDaoBase.SelectALL<TProduto>
    .Where(['CODIGO'], ['123456'])
    .Get();
end;

procedure TestTDaoBase.PodeAtualizarProduto;
var
  Produto: TProduto;
  ProdutoBD: TProduto;
begin
  // ambiente
  Produto := TSeed.ProdutoTeste();

  FDaoBase.Insert<TProduto>(Produto);

  ProdutoBD := FDaoBase.SelectALL<TProduto>
    .Where(['CODIGO'], [Produto.CODIGO]).Get();
  CheckNotNull(ProdutoBD);
  CheckProdutos(Produto, ProdutoBD);

  // teste
  Produto.DESCRICAO := 'Descrição Atualizada!!!!';
  Produto.BLOQUEADO := True;
  Produto.PRECO_CUSTO := 1.99;
  Produto.DATA_CADASTRO := date;
  Produto.TESTENULLSTRING := 'string não é nula';
  Produto.TESTENULLINTEGER.Value := 10;
  Produto.TESTENULLDATE.Value := StrToDate('13/04/2023');
  Produto.TESTENULLNUMERIC.Value := 5.69;

  FDaoBase.Update<TProduto>(Produto);

  // assertivas
  FreeAndNil(ProdutoBD);
  ProdutoBD := FDaoBase.SelectALL<TProduto>.Where(['CODIGO'], [Produto.CODIGO]).Get();

  CheckNotNull(ProdutoBD);
  CheckProdutos(Produto, ProdutoBD);

end;

procedure TestTDaoBase.PodeAtualizarProdutoSQlBuilder;
var
  Produto: TProduto;
  ProdutoBD: TProduto;
  CODIGO, DESCRICAO: string;
begin
  // ambiente
  Produto := TSeed.ProdutoTeste();
  CODIGO := Produto.CODIGO;
  FDaoBase.Insert<TProduto>(Produto);

  // confirmar que o produto está no banco de dados
  ProdutoBD := FDaoBase.SelectALL<TProduto>
    .Where(['CODIGO'], [CODIGO])
    .Get();
  CheckNotNull(ProdutoBD);
  FreeAndNil(ProdutoBD);

  DESCRICAO := 'ALterado';
  Produto.DESCRICAO := DESCRICAO;

  Produto.UND := 'CXA';

  // teste
  FDaoBase.Update<TProduto>
    .ColumnSetValue('DESCRICAO', DESCRICAO)
    .ColumnSetValue('UND', 'CXA')
    .Where('CODIGO').Equal(CODIGO)
    .&And('UND').Equal('UN')
    .Exec();

  // assertiva
  ProdutoBD := FDaoBase.SelectALL<TProduto>
    .Where('CODIGO').Equal(CODIGO)
    .Get();

  CheckNotNull(ProdutoBD);
  CheckEquals(ProdutoBD.DESCRICAO, DESCRICAO);

  // CheckEquals(FDaoBase.LastSql, 'Update ' + #13#10 + ' *' + #13#10 +
  // ' PRODUTO set  (DESCRICAO Like :DESCRICAO) And (UND = :UND) And (DATA_CADASTRO >= :DATA_CADASTRO) And (PRECO_VENDA > :PRECO_VENDA) And (PRECO_ATACADO <= :PRECO_ATACADO) Or (BLOQUEADO = :BLOQUEADO) Order By DATA_CADASTRO');

end;

procedure TestTDaoBase.PodeDarSelectEmObjetoSemAnotacao;
var
  tmovs: TList<TMOV>;
begin
  // tmovs := FDaoBase.SelectALL<TMOV>      .ToList;

  // CheckTrue(tmovs.Count > 0);
end;

procedure TestTDaoBase.PodeExcluirProduto;
var
  Produto: TProduto;
  ProdutoBD: TProduto;
  CODIGO: string;
begin
  // ambiente
  Produto := TSeed.ProdutoTeste();
  CODIGO := Produto.CODIGO;
  FDaoBase.Insert<TProduto>(Produto);

  // confirmar que o produto está no banco de dados
  ProdutoBD := FDaoBase.SelectALL<TProduto>
    .Where(['CODIGO'], [CODIGO])
    .Get();
  CheckNotNull(ProdutoBD);
  FreeAndNil(ProdutoBD);

  // teste
  FDaoBase.Delete<TProduto>(Produto);

  // assertiva
  ProdutoBD := FDaoBase.SelectALL<TProduto>
    .Where(['CODIGO'], [CODIGO])
    .Get();
  CheckNull(ProdutoBD);

end;

procedure TestTDaoBase.PodeExcluirProdutoSQLBuilder;
var
  Produto: TProduto;
  ProdutoBD: TProduto;
  CODIGO: string;
begin
  // ambiente
  Produto := TSeed.ProdutoTeste();
  CODIGO := Produto.CODIGO;
  FDaoBase.Insert<TProduto>(Produto);

  // confirmar que o produto está no banco de dados
  ProdutoBD := FDaoBase.SelectALL<TProduto>
    .Where(['CODIGO'], [CODIGO])
    .Get();
  CheckNotNull(ProdutoBD);
  FreeAndNil(ProdutoBD);

  // teste
  FDaoBase.Delete<TProduto>()
    .Where('CODIGO').Equal(CODIGO)
    .Exec;

  // assertiva
  ProdutoBD := FDaoBase.SelectALL<TProduto>
    .Where(['CODIGO'], [CODIGO])
    .Get();
  CheckNull(ProdutoBD);
end;

procedure TestTDaoBase.CheckProdutos(Produto: TProduto; ProdutoBD: TProduto);
begin
  CheckEquals(Produto.CODIGO, ProdutoBD.CODIGO);
  CheckEquals(Produto.BARRAS, ProdutoBD.BARRAS);
  CheckEquals(Produto.DESCRICAO, ProdutoBD.DESCRICAO);
  CheckEquals(Produto.UND, ProdutoBD.UND);
  CheckEquals(Produto.CUSTO_MEDIO, ProdutoBD.CUSTO_MEDIO);
  CheckEquals(Produto.PRECO_CUSTO, ProdutoBD.PRECO_CUSTO);
  CheckEquals(Produto.PRECO_VENDA, ProdutoBD.PRECO_VENDA);
  CheckEquals(Produto.PRECO_ATACADO, ProdutoBD.PRECO_ATACADO);
  CheckEquals(Produto.MARGEM_LUCRO, ProdutoBD.MARGEM_LUCRO);
  CheckEquals(Produto.ALTERACAO_PRECO, ProdutoBD.ALTERACAO_PRECO);
  CheckEquals(Produto.ULTIMA_COMPRA, ProdutoBD.ULTIMA_COMPRA);
  CheckEquals(DateTimeToStr(Produto.ULTIMA_VENDA), DateTimeToStr(ProdutoBD.ULTIMA_VENDA));
  CheckEquals(Produto.DATA_CADASTRO, ProdutoBD.DATA_CADASTRO);
  CheckEquals(Produto.BLOQUEADO, ProdutoBD.BLOQUEADO);
  CheckEquals(Produto.OBSERVACOES, ProdutoBD.OBSERVACOES);
  CheckEquals(Produto.QUANTIDADEFRACIONADA, ProdutoBD.QUANTIDADEFRACIONADA);
  CheckEquals(Produto.TESTENULLSTRING.HasValue, ProdutoBD.TESTENULLSTRING.HasValue);
  CheckEquals(Produto.TESTENULLINTEGER.HasValue, ProdutoBD.TESTENULLINTEGER.HasValue);
  CheckEquals(Produto.TESTENULLDATE.HasValue, ProdutoBD.TESTENULLDATE.HasValue);
  CheckEquals(Produto.TESTENULLNUMERIC.HasValue, ProdutoBD.TESTENULLNUMERIC.HasValue);

end;

procedure TestTDaoBase.PodeInserirAtualizarApagarPedido;
var
  Pedido: TPedido;
  Item: TItemPedido;
  ItemBD: TItemPedido;
  Lista: TList<TItemPedido>;
  Produto: TProduto;
  sqlEsperado: string;
  I: Integer;
begin
  Pedido := TSeed.PedidoTeste;
  // teste
  FDaoBase.Insert<TPedido>(Pedido);

  for Item in Pedido.Itens do
  begin
    Produto := TSeed.ProdutoTeste();
    FDaoBase.Insert<TProduto>(Produto);

    Item.IDPEDIDO := Pedido.ID;
    Item.CODPRODUTO := Produto.CODIGO;
    Item.DESCRICAO := Produto.BARRAS;
    Item.UND := Produto.UND;

    FDaoBase.Insert<TItemPedido>(Item);

    // verificar se o item foi gravado
    ItemBD := FDaoBase.SelectALL<TItemPedido>
      .Where(['IDPEDIDO', 'SEQ'], [Item.IDPEDIDO, Item.SEQ])
      .Get();
    CheckNotNull(ItemBD);
    CheckItemPedido(Item, ItemBD);

    ItemBD.Free;
  end;

  Lista := FDaoBase.SelectALL<TItemPedido>
    .Where(['IDPEDIDO'], [Pedido.ID])
    .OrderBy('SEQ')
    .ToList();

  // CheckEquals(FDaoBase.LastSql, sqlEsperado);
  CheckEquals(Lista.Count, Pedido.ItemCount);

  for I := 0 to Pred(Lista.Count) do
  begin
    Item := Pedido.Itens[I];
    ItemBD := Lista[I];
    CheckItemPedido(Item, ItemBD);
  end;

  // alterar o item
  Item := Pedido.Itens[2];
  Item.DESCRICAO := 'Descrição alterada!!!!';
  Item.VALOR_UNITA := 5.69;

  FDaoBase.Update<TItemPedido>(Item);

  // verificar se o item foi alterado
  ItemBD := FDaoBase.SelectALL<TItemPedido>
    .Where(['IDPEDIDO', 'SEQ'], [Item.IDPEDIDO, Item.SEQ])
    .Get();
  CheckNotNull(ItemBD);
  CheckItemPedido(Item, ItemBD);

  // deletar o item
  Item := Pedido.Itens[2];
  FDaoBase.Delete<TItemPedido>(Item);

  sqlEsperado := Format('Delete From ITEMPEDIDO Where (IDPEDIDO = %d) And (SEQ = %d)', [Item.IDPEDIDO, Item.SEQ]);
  CheckEquals(FDaoBase.LastSql, sqlEsperado);

  // verificar se o item foi apagado
  ItemBD := FDaoBase.SelectALL<TItemPedido>
    .Where(['IDPEDIDO', 'SEQ'], [Item.IDPEDIDO, Item.SEQ])
    .Get();
  CheckNull(ItemBD);

  // deletar o item
  Item := Pedido.Itens[0];
  FDaoBase.Delete<TItemPedido>(Item);

  sqlEsperado := Format('Delete From ITEMPEDIDO Where (IDPEDIDO = %d) And (SEQ = %d)', [Item.IDPEDIDO, Item.SEQ]);
  CheckEquals(FDaoBase.LastSql, sqlEsperado);

  // verificar se o item foi apagado
  ItemBD := FDaoBase.SelectALL<TItemPedido>
    .Where(['IDPEDIDO', 'SEQ'], [Item.IDPEDIDO, Item.SEQ])
    .Get();
  CheckNull(ItemBD);

  // deletar o item
  Item := Pedido.Itens[1];
  FDaoBase.Delete<TItemPedido>(Item);

  sqlEsperado := Format('Delete From ITEMPEDIDO Where (IDPEDIDO = %d) And (SEQ = %d)', [Item.IDPEDIDO, Item.SEQ]);
  CheckEquals(FDaoBase.LastSql, sqlEsperado);

  // verificar se o item foi apagado
  ItemBD := FDaoBase.SelectALL<TItemPedido>
    .Where(['IDPEDIDO', 'SEQ'], [Item.IDPEDIDO, Item.SEQ])
    .Get();
  CheckNull(ItemBD);

end;

procedure TestTDaoBase.CheckItemPedido(Item: TItemPedido; ItemBD: TItemPedido);
begin
  CheckEquals(Item.SEQ, ItemBD.SEQ);
  CheckEquals(Item.IDPEDIDO, ItemBD.IDPEDIDO);
  CheckEquals(Item.CODPRODUTO, ItemBD.CODPRODUTO);
  CheckEquals(Item.DESCRICAO, ItemBD.DESCRICAO);
  CheckEquals(Item.UND, ItemBD.UND);
  CheckEquals(Item.QTD, ItemBD.QTD);
  CheckEquals(Item.VALOR_UNITA, ItemBD.VALOR_UNITA);
  CheckEquals(Item.VALOR_DESCONTO, ItemBD.VALOR_DESCONTO);
  CheckEquals(Item.VALOR_TOTAL, ItemBD.VALOR_TOTAL);
  CheckEquals(Item.STATUS, ItemBD.STATUS);

end;

procedure TestTDaoBase.PodeInserirProduto;
var
  Produto: TProduto;
  ProdutoBD: TProduto;
begin
  // ambiente
  Produto := TSeed.ProdutoTeste();

  // teste
  FDaoBase.Insert<TProduto>(Produto);

  // assertivas
  ProdutoBD := FDaoBase.SelectALL<TProduto>
    .Where(['CODIGO'], [Produto.CODIGO])
    .Get();
  CheckNotNull(ProdutoBD);
  CheckEquals(TSeed.GetXML, ProdutoBD.MENSAGEMRETORNO);
  CheckProdutos(Produto, ProdutoBD);
  CheckEquals(Produto.TESTENULLSTRING.HasValue, ProdutoBD.TESTENULLSTRING.HasValue);

end;

procedure TestTDaoBase.PodeRetornarClientDataSet;
var
  Produto: TProduto;
begin
  // ambiente
  Produto := TSeed.ProdutoTeste();
  FDaoBase.Insert<TProduto>(Produto);

  // teste
  var
  cd := FDaoBase
    .SelectALL<TProduto>
    .ToAdapter
    .AsClientDataset;

  CheckNotNull(cd);
  CheckTrue(cd.RecordCount > 0);
  cd.Free;
end;

procedure TestTDaoBase.GetProdutoMaisDeDoisParametros;
var
  Produto: TProduto;
begin
  Produto := FDaoBase.SelectALL<TProduto>
    .Where(['CODIGO', 'UND', 'DESCRICAO'], [123456, 'UN', 'TESTE'])
    .Get();
end;

initialization

// Register any test cases with the test runner
RegisterTest(TestTDaoBase.Suite);

end.
